def info = rootProject.ext.modules.get(project.name)
if (info.alone) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    resourcePrefix project.name + "_"
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 100
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def baseName = "base"
def baseInfo = rootProject.ext.modules.get(baseName)

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api rootProject.ext.deps.kotlin
    if (baseInfo.alone) {
        api group: rootProject.ext.moduleGroup, name: baseName, version: baseInfo.version
    } else {
        api project(":$baseName")
    }

    //WMRouter
    kapt rootProject.ext.deps.wm_router_compiler

    testImplementation rootProject.ext.deps.junit
    androidTestImplementation rootProject.ext.deps.runner
    androidTestImplementation rootProject.ext.deps.espresso
}